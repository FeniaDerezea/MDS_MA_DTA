model{


for(i in 1:nrow){
	

                n[i,1] <- N[i] 
		p[i,1] <- pr[Studyc[i],State[i],1] 

		for(t in 2:Tc[i]){       
			n[i,t] <- x[i,t-1]  		
			p[i,t] <-  pr[Studyc[i],State[i],t] / pr[Studyc[i],State[i],t-1]	 
			}    
	

	         for(t in 1:Tc[i]){
			x[i,t] ~ dbin(p[i,t], n[i,t])  

                        bc[i,t] <- (( pow(C[i,t], lambda) - 1 ) / lambda )*(1 - equals(lambda, 0)) 
			 +  log(C[i,t])*equals(lambda, 0)  	# Box-Cox transformation
                        #bc[i,t] <-log(C[i,t])
	                           
			d[i,t] <- (mu[Studyc[i],State[i]] - bc[i,t] ) / s[Studyc[i],State[i]] 
			logit(pr[Studyc[i],State[i],t]) <- min(10, max(-10, d[i,t]) )  

                        xhat[i,t] <- p[i,t]*n[i,t]  		# Fitted values

                       dev[i,t] <- 2*(x[i,t]*(log(x[i,t]) - log(xhat[i,t])) + (n[i,t] - x[i,t])*(log(n[i,t] - x[i,t])     
                                  - log(n[i,t] - xhat[i,t])))  # Residual deviance contribution
                        }

rd[i] <- sum(dev[i,1:Tc[i]]) 	# Residual deviance study i
		
}

for(i in 1:ntypeall){
	

                nall[i,1] <- Nall[i] 
		pall[i,1] <- prall[i,1] 

		for(t in 2:Tcall[i]){       
			nall[i,t] <- xall[i,t-1]  		
			pall[i,t] <-  prall[i,t] / prall[i,t-1]	 
			}    
	

	         for(t in 1:Tcall[i]){
			xall[i,t] ~ dbin(pall[i,t], nall[i,t])  
                        prall[i,t] <-inprod(q[i,1:(J-1)],pr[Studyallc[i],2:J,t])

                         bcall[i,t] <- (( pow(Call[i,t], lambda) - 1 ) / lambda )*(1 - equals(lambda, 0)) 
			 +  log(Call[i,t])*equals(lambda, 0)  	# Box-Cox transformation
                         #bcall[i,t] <-log(Call[i,t])

                        	                           
			for (j in 1:(J-1)){
                          dall[i,t,j] <- (mu[Studyallc[i],j+1] - bcall[i,t] ) / s[Studyallc[i],j+1]  
                          logit(pr[Studyallc[i],j+1,t]) <- min(10, max(-10, dall[i,t,j]) )
                        }


                        xhatall[i,t] <- pall[i,t]*nall[i,t]  		# Fitted values

                       devall[i,t] <- 2*(xall[i,t]*(log(xall[i,t]) - log(xhatall[i,t])) + (nall[i,t] - xall[i,t])*(log(nall[i,t] - xall[i,t])     
                                  - log(nall[i,t] - xhatall[i,t])))  # Residual deviance contribution


                        }
rdall[i] <- sum(devall[i,1:Tcall[i]]) 	# Residual deviance study i
		
}


for(i in 1:ntype0a){
	

                n0a[i,1] <- N0a[i] 
		p0a[i,1] <- pr0a[i,1] 

		for(t in 2:Tc0a[i]){       
			n0a[i,t] <- x0a[i,t-1]  		
			p0a[i,t] <-  pr0a[i,t] / pr0a[i,t-1]	 
			}    
	

	         for(t in 1:Tc0a[i]){
			x0a[i,t] ~ dbin(p0a[i,t], n0a[i,t])  
                        pr0a[i,t] <-qq[i]*pr[Study0ac[i],2,t]+(1-qq[i])*pr[Study0ac[i],3,t]

                         bc0a[i,t] <- (( pow(C0a[i,t], lambda) - 1 ) / lambda )*(1 - equals(lambda, 0)) 
			 +  log(C0a[i,t])*equals(lambda, 0)  	# Box-Cox transformation
                         #bc0a[i,t] <- log(C0a[i,t])

                        for (j in 1:2){
                          d0a[i,t,j] <- (mu[Study0ac[i],j+1] - bc0a[i,t] ) / s[Study0ac[i],j+1]  
                          logit(pr[Study0ac[i],j+1,t]) <- min(10, max(-10, d0a[i,t,j]) )
                        }
	                           
			

                         xhat0a[i,t] <- p0a[i,t]*n0a[i,t]  		# Fitted values

                       dev0a[i,t] <- 2*(x0a[i,t]*(log(x0a[i,t]) - log(xhat0a[i,t])) + (n0a[i,t] - x0a[i,t])*(log(n0a[i,t] - x0a[i,t])     
                                  - log(n0a[i,t] - xhat0a[i,t])))  # Residual deviance contribution
                        }
qq[i] ~ dunif(0,1)
rd0a[i] <- sum(dev0a[i,1:Tc0a[i]]) 	# Residual deviance study i		
}

for(i in 1:ntypeall2){
	

                nall2[i,1] <- Nall2[i] 
		pall2[i,1] <- prall2[i,1] 

		for(t in 2:Tcall2[i]){       
			nall2[i,t] <- xall2[i,t-1]  		
			pall2[i,t] <-  prall2[i,t] / prall2[i,t-1]	 
			}    
	

	         for(t in 1:Tcall2[i]){
			xall2[i,t] ~ dbin(pall2[i,t], nall2[i,t])  
                        prall2[i,t] <-q0a[i]*pr0a[i+ntype0a,t]+qbcd2[i]*pr[Studyall2c[i],4,t]
                        pr0a[i+ntype0a,t]<-qq2[i]*pr[Studyall2c[i],2,t]+(1-qq2[i])*pr[Studyall2c[i],3,t]

                         bcall2[i,t] <- (( pow(Call2[i,t], lambda) - 1 ) / lambda )*(1 - equals(lambda, 0)) 
			 +  log(Call2[i,t])*equals(lambda, 0)  	# Box-Cox transformation
                         #bcall2[i,t] <- log(Call2[i,t])

	                 for (j in 1:(J-1)){
                          dall2[i,t,j] <- (mu[Studyall2c[i],j+1] - bcall2[i,t] ) / s[Studyall2c[i],j+1]  
                          logit(pr[Studyall2c[i],j+1,t]) <- min(10, max(-10, dall2[i,t,j]) )
                        }
          
			
                        xhatall2[i,t] <- pall2[i,t]*nall2[i,t]  		# Fitted values

                       devall2[i,t] <- 2*(xall2[i,t]*(log(xall2[i,t]) - log(xhatall2[i,t])) + (nall2[i,t] - xall2[i,t])*(log(nall2[i,t] - xall2[i,t])     
                                  - log(nall2[i,t] - xhatall2[i,t])))  # Residual deviance contribution


                        }
qq2[i] ~ dunif(0,1)
rdall2[i] <- sum(devall2[i,1:Tcall2[i]]) 	# Residual deviance study i
		
}






for (i in 1:ntot){
mu[i,1:4] ~ dmnorm(mean[1:4],Prec[1:4,1:4])


for (r in 1:4){
s[i,r]<-exp(logs[i,r])
logs[i,r] ~ dnorm(ms[r],precs[r])
}


}


# Priors:
lambda ~ dunif(-3,3) # comment this out if log-link used


Prec[1:4,1:4] ~ dwish(R[,],4)
 tau[1:4,1:4] <- inverse(Prec[1:4,1:4])


        for(r in 1:4){
	sds[r] ~ dunif(0,5) 
	precs[r] <- pow(sds[r], -2)

	}

       	mean[1] ~ dnorm(0, 0.01)
       	mean[2] ~ dnorm(0, 0.01)
        mean[3] ~ dnorm(0, 0.01)T(mean[2],)
        mean[4] ~ dnorm(0, 0.01)T(mean[3],)
        ms[1] ~ dnorm(0, 0.01) 
        ms[2] ~ dnorm(0, 0.01) 
        ms[3] ~ dnorm(0, 0.01)T(ms[2],)
        ms[4] ~ dnorm(0, 0.01)T(ms[3],)


resdev <- sum(rd[])+sum(rdall[]) +sum(rd0a[]) +sum(rdall2[]) # Total residual deviance



}
